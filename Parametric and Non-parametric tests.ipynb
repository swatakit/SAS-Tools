{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "<font face=\"Verdana, cursive, sans-serif\" >\n",
    "<center><H1>Parametric and Non-parametric tests</H1></center>\n",
    "\n",
    "<center><H2><font color='darkred'>How to quickly identify potential changes in the behavior of a service provider</font></H2></center>\n",
    "\n",
    "<p>This documentation is powered by <b>Jupyter Notebooks</b>. To learn more about how to code SAS in Jupyter Notebooks environment, please refer to <a href=\"https://github.com/sassoftware/sas_kernel\">SAS Kernel for Jupyter</a>. Please note that you - DO NOT -  required to have Python or Jupyter Notebooks in order to utilize these <b>SAS</b> macros.</p>\n",
    "\n",
    "<p>Imagine you have thousands of service providers(hospitals, clinics, labs, garages, etc..), how can we prioritize and pick ones for auditing/investigating?</p> \n",
    "\n",
    "<p>There are numbers of reasons that the costs of services would changes: increases or decreases, but before we even deep dive into the reason <b>why?</b> we first need to identify <b>who?</b>. Luckily, we have plenty of statistical tools that can help address the later problem. The former problem, however, is not as straight forward as the other one but still possible with helps from subject matter experts(SMEs)...</p>\n",
    "\n",
    "<H3>The Who?</H3>\n",
    "\n",
    "The key to understanding data is to understand its distribution. The shape of the distribution is usually governed by a few parameters. The one most commonly known distribution is Gaussian distribution, aka normal distribution, of which the shape of the distribution is governed by the mean and the variance.\n",
    "\n",
    "The mean(+ or -) moves the bell curve to the (right or left), while the variance(high or low) controls the spread(wide or narrow) of the bell curve.\n",
    "<img src=\"./images/normal.png\" >\n",
    "ref: https://en.wikipedia.org/wiki/Normal_distribution\n",
    "\n",
    "That is, if we would like to know whether 2 variable is randomly sampling from the same normally distributed population, we test whether the 2 distributions are statistically different or not.\n",
    "<br>If the data is continuous, and the normal distribution assumption holds, the following are the common parametric tests that we can choose from...\n",
    "<img src=\"./images/ttest.png\" style=\"width: 50%\">\n",
    "ref: http://www.indiana.edu/~statmath/\n",
    "\n",
    "\n",
    "<table >\n",
    "  <tr>\n",
    "    <th style=\"text-align: left; vertical-align: middle;\">Parametric Tests</th>\n",
    "    <th style=\"text-align: left; vertical-align: middle;\">When and Why</th> \n",
    "  </tr>\n",
    "  <tr>\n",
    "    <td style=\"text-align: left; vertical-align: middle;\">One sample t-test</td>\n",
    "    <td style=\"text-align: left; vertical-align: middle;\">You would like to test whether the mean of a sample is equal to a constant 'c'; that is to say, Is this data randomly sampling from a population which has a mean of  'c'</td>\n",
    "  </tr>\n",
    "  <tr>\n",
    "    <td style=\"text-align: left; vertical-align: middle;\">Pair sample t-test</td>\n",
    "    <td style=\"text-align: left; vertical-align: middle;\">You have 2 samples and you would like to test whether the distribution of these 2 data is the same. However, there is some correlation/dependence between these 2 samples. For example, you have a group of student do a test, then provide the training, and have them redo the test again afterward, and then compare the difference of the score before/after. </td>\n",
    "  </tr>\n",
    "  <tr>\n",
    "    <td style=\"text-align: left; vertical-align: middle;\">Independent sample t-test</td>\n",
    "    <td style=\"text-align: left; vertical-align: middle;\">You have 2 samples and you would like to test whether the distribution of these 2 data is the same. And, these 2 samples are not at all correlated/dependent to one another. For example, you have a group of female students and male students. You would like to test whether the average test score of the female is statistically different from male</td>\n",
    "  </tr>\n",
    "  <tr>\n",
    "    <td style=\"text-align: left; vertical-align: middle;\">ANOVA</td>\n",
    "    <td style=\"text-align: left; vertical-align: middle;\">ANalysis Of VAriance. Instead of compare means of 2 samples, you can also compare multiple means of multiple samples at the same time. Rejecting the null hypothesis means there is at least one mean that statistically different than others.</td>\n",
    "  </tr>\n",
    "</table>\n",
    "\n",
    "<b>But what if normally distributed assumption does not hold?</b>\n",
    "\n",
    "Outliers can cause the distribution to skewed. Data transformation can also make the data more conform to normal distribution. Such transformation methods are: outliers removal, square root, winzorization, logarithmic, logit;Among many other methods.\n",
    "\n",
    "Yet, sometimes, even transformation cannot help. If, let's say, you have ordinal data, ranked data or you have very small dataset and cannot afford to loose any observations.\n",
    "\n",
    "\n",
    "<b>The non-parametric tests</b>\n",
    "\n",
    "If the data is not(or lack of) normally distributed, non-parametric tests are the alternative. Non-parametric tests are sometimes called distribution-free tests because it does not assume any shapes of the data.\n",
    "\n",
    "However, it is also important to note that the power of non-parametric tests is much less. Meaning that it is much harder to reject the null hypothesis. The following table shows comparable tests between parametric and non-parametric tests.\n",
    "\n",
    "\n",
    "<table >\n",
    "  <tr>\n",
    "    <th style=\"text-align: left; vertical-align: middle;\">Parametric Tests</th>\n",
    "    <th style=\"text-align: left; vertical-align: middle;\">When and Why</th> \n",
    "  </tr>\n",
    "  <tr>\n",
    "    <td style=\"text-align: left; vertical-align: middle;\">One sample t-test</td>\n",
    "    <td style=\"text-align: left; vertical-align: middle;\">Sign ranked test</td>\n",
    "  </tr>\n",
    "  <tr>\n",
    "    <td style=\"text-align: left; vertical-align: middle;\">Pair sample t-test</td>\n",
    "    <td style=\"text-align: left; vertical-align: middle;\">Wilcoxon signed-rank test</td>\n",
    "  </tr>\n",
    "  <tr>\n",
    "    <td style=\"text-align: left; vertical-align: middle;\">Independent sample t-test</td>\n",
    "    <td style=\"text-align: left; vertical-align: middle;\">Mann-Whitney U Test</td>\n",
    "  </tr>\n",
    "  <tr>\n",
    "    <td style=\"text-align: left; vertical-align: middle;\">ANOVA</td>\n",
    "    <td style=\"text-align: left; vertical-align: middle;\">Kruskal-Wallis Test</td>\n",
    "  </tr>\n",
    "</table>\n",
    "\n",
    "** Very very important note **\n",
    "Non-parametric tests are distribution-free, not assumption-free. \n",
    "Other assumption like randomness, iid, and etc still stands - please study carefully full explanation <a href=\"http://sphweb.bumc.bu.edu/otlt/mph-modules/bs/bs704_nonparametric/BS704_Nonparametric_print.html\">here</a>.\n",
    "\n",
    "<img src=\"./images/comp.png\" style=\"width: 70%\" >\n",
    "An example of a service provider. By comparing claim costs 2016 vs. 2017, it seems that the year 2017 has more expensive claims. Wilcoxon tests also confirm that the two distribution are statistically different. The mean and median of 2017 also increases."
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "<font face=\"Verdana, cursive, sans-serif\" >\n",
    "<H2><font color='darkred'>Part-1: Simple data exploration, parametric and non-parametric test </font></H2>"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "<font face=\"Verdana, cursive, sans-serif\" >\n",
    "<b>First, let's take a look at a mockup data </b>"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "OPTION NOSOURCE NONOTES;\n",
    "\n",
    "DATA WORK.CLAIMDATA ;\n",
    "INFILE \"C:\\sasdata\\claimdata_mockup.csv\" DELIMITER\t= ',' MISSOVER DSD LRECL=32767 FIRSTOBS=2;\n",
    "INFORMAT Provider $1.  ClaimAmt Los best32. Year best32.;\n",
    "INPUT Provider$ ClaimAmt Los Year;\n",
    "RUN;\n",
    "\n",
    "PROC SORT DATA=CLAIMDATA; BY Provider Year ;RUN;\n",
    "\n",
    "PROC TABULATE DATA=CLAIMDATA MISSING;\n",
    "CLASS YEAR Provider;\n",
    "VAR ClaimAmt Los;\n",
    "TABLE Provider, YEAR*ClaimAmt*(N MEAN MEDIAN);\n",
    "RUN;"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "<font face=\"Verdana, cursive, sans-serif\" >\n",
    "<img src=\"./images/basic_stats.PNG\" >\n",
    "The above table shows that we have 8 providers. We have providers with increasing claim costs trends as well as decreasing claim costs trends. Let's check whether the data distribution is normally distributed."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "*Take provider A and Year 2016 as an example;\n",
    "\n",
    "PROC UNIVARIATE DATA=CLAIMDATA NORMAL PLOT;\n",
    "VAR ClaimAmt;\n",
    "WHERE Provider='A' AND YEAR=2016;\n",
    "HISTOGRAM/NORMAL;\n",
    "RUN; \n"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "<font face=\"Verdana, cursive, sans-serif\" >\n",
    "<img src=\"./images/norm_test.PNG\" style=\"width: 30%\">\n",
    "The above table shows that all p-values are rejected. That is, the data distribution is **NOT** normally distributed.\n",
    "    \n",
    "<p>Next, let's utilize PROC NPAR1WAY for non-parametric tests </p>"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "PROC NPAR1WAY DATA=CLAIMDATA Wilcoxon EDF ;\n",
    "CLASS YEAR;\n",
    "VAR ClaimAmt;\n",
    "WHERE Provider='A' and YEAR in (2016 2017);\n",
    "*EXACT; *Use this option for small sample or heaviliy skewed data, but be caution of heavy computaional requirements;\n",
    "OUTPUT OUT=RESULTS;\n",
    "RUN;\n",
    "\n",
    "PROC PRINT DATA=RESULTS;RUN;"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "<font face=\"Verdana, cursive, sans-serif\" >\n",
    "<img src=\"./images/nonparam_test.PNG\" style=\"width: 90%\" >\n",
    "\n",
    "2 out of 4 non-parametrics tests are significant at alpha = 5%. We can conclude that claim distribution of provider A of year 2016 is statistically different. And, from previous basic stats, both mean and total claim in 2017 is greather than 2016"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "<font face=\"Verdana, cursive, sans-serif\" >\n",
    "<H2><font color='darkred'>Part-2: SAS macro to perform non-parametric tests for all providers </font></H2>\n",
    "\n",
    "The following macro is the exact calculation shown in previous section, only implemented in such a way that we can quickly perform multiple tests on multiple provider at the same time, and then categorize them into bins"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "******************\n",
    "Define macros\n",
    "******************;\n",
    "\n",
    "\n",
    "%macro QUICK_LEFTJOIN(LTAB,RTAB,KEY,OUT=JOINED);\n",
    "PROC SQL;\n",
    "CREATE TABLE &OUT. AS\n",
    "(\n",
    "SELECT *\n",
    "FROM &LTAB. LTAB\n",
    "LEFT JOIN &RTAB. RTAB\n",
    "ON LTAB.&KEY.=RTAB.&KEY.\n",
    ") \n",
    ";RUN;QUIT;\n",
    "%mend;\n",
    "\n",
    "%macro NONPARAMETRIC_TESTS(DSNAME=,VAR=,BY=,GROUP=,OUT=NONPARA_TEST,CRI=0.05);\n",
    "\n",
    "PROC SQL NOPRINT  ;\n",
    "SELECT MAX(YEAR) AS YEAR\n",
    "\tINTO: LATEYEAR\n",
    "\tFROM &DSNAME.;\n",
    ";QUIT;\n",
    "\n",
    "ODS GRAPHICS OFF;\n",
    "PROC SORT DATA= &DSNAME.; BY &BY. ;RUN;\n",
    "PROC NPAR1WAY DATA=&DSNAME. Wilcoxon EDF  NOPRINT;\n",
    "CLASS &GROUP. ;\n",
    "VAR &VAR.;\n",
    "BY &BY.;\n",
    "OUTPUT OUT=NPAR(KEEP=&BY. PT2_WIL P_KW P_KSA P_KA RENAME=(PT2_WIL=Wilcoxon P_KW=KruskalWallis P_KSA=KolmogorovSmirnof P_KA=Kupiec));\n",
    "RUN;\n",
    "\n",
    "PROC MEANS DATA=&DSNAME. NOPRINT ;\n",
    "VAR &VAR.;\n",
    "CLASS &GROUP. &BY.;\n",
    "OUTPUT N=N MEAN=MEAN SUM=SUM OUT=STATS(WHERE=(NOT MISSING(&GROUP.) AND NOT MISSING(&BY.)) DROP=_TYPE_ _FREQ_);\n",
    "RUN;\n",
    "\n",
    "PROC SORT DATA=STATS; BY &BY. &GROUP. ; RUN;\n",
    "\n",
    "DATA BSTATS(KEEP= N MEAN SUM &BY. Change_ClaimCnt Change_AvgClaimAmt Change_TotClaimAmt  \n",
    "RENAME=(N=LateYear_ClaimCnt MEAN=LateYear_AvgClaimAmt SUM=LateYear_TotClaimAmt));\n",
    "SET STATS;\n",
    "BY &BY. &GROUP. ;\n",
    "Change_ClaimCnt=LOG(N/LAG(N));\n",
    "Change_AvgClaimAmt=LOG(MEAN/LAG(MEAN));\n",
    "Change_TotClaimAmt=LOG(SUM/LAG(SUM));\n",
    "IF FIRST.&BY. THEN \n",
    "\tDO; \n",
    "\t\tChange_ClaimCnt=.;\n",
    "\t\tChange_AvgClaimAmt=.; \t\n",
    "\t\tChange_TotClaimAmt=.;\n",
    "\tEND;\n",
    "IF LAST.&BY.;\n",
    "IF MISSING(Change_ClaimCnt) AND MISSING(Change_AvgClaimAmt) AND MISSING(Change_TotClaimAmt) THEN DELETE;\n",
    "\n",
    "RUN;\n",
    "\n",
    "%QUICK_LEFTJOIN(LTAB=BSTATS,RTAB=NPAR,KEY=&BY.,OUT=&OUT.);\n",
    "\n",
    "DATA &OUT.;\n",
    "SET &OUT.;\n",
    "IF NMISS(Wilcoxon,KruskalWallis,KolmogorovSmirnof,Kupiec)=0 THEN Significant=SUM(Wilcoxon<&CRI.,KruskalWallis<&CRI.,KolmogorovSmirnof<&CRI., Kupiec<&CRI.);\n",
    "ELSE Significant=0;\n",
    "IF Significant>=2 THEN FLAG_ISSIGNIFICANT=1; ELSE  FLAG_ISSIGNIFICANT=0;\n",
    "RUN;\n",
    "\n",
    "PROC FORMAT;\n",
    "VALUE PCT_RANGE \n",
    ". = 'Z_ERROR'\n",
    "LOW-0 = '0_Reduced'\n",
    "0-0.1 = 'A_(<10%]'\n",
    "0.1-0.2 = 'B_(10%-20%]'\n",
    "0.2-0.3='C_(20%-30%]'\n",
    "0.3-0.4='D_(30%-40%]'\n",
    "0.4-0.5='E_(40%-50%]'\n",
    "0.5-HIGH = 'F_(50%>)' \n",
    ";\n",
    "RUN;\n",
    "\n",
    "Footnote If 2 out of 4 non-parametric tests are significant at alpha &CRI. - Flag=1;\n",
    "TITLE3 Distribution of All Providers;\n",
    "PROC TABULATE DATA= &out. MISSING;\n",
    "FORMAT Change_TotClaimAmt Change_AvgClaimAmt PCT_RANGE. ;\n",
    "CLASS Change_TotClaimAmt Change_AvgClaimAmt FLAG_ISSIGNIFICANT ;\n",
    "TABLE (FLAG_ISSIGNIFICANT*Change_AvgClaimAmt)ALL,Change_TotClaimAmt ALL PCTN;\n",
    "RUN;\n",
    "\n",
    "DATA HOSP_TAG(KEEP=FLAG_ISSIGNIFICANT Provider Avg_BIN Tot_BIN);\n",
    "SET &out.;\n",
    "Avg_BIN=PUT(Change_AvgClaimAmt,PCT_RANGE.);\n",
    "Tot_BIN=PUT(Change_TotClaimAmt,PCT_RANGE.);\n",
    "RUN;\n",
    "\n",
    "%QUICK_LEFTJOIN(LTAB=&DSNAME.,RTAB=HOSP_TAG,KEY=Provider,OUT=JOINED);\n",
    "\n",
    "TITLE3 Average Length of Stay ;\n",
    "PROC TABULATE DATA= JOINED ;\n",
    "CLASS Tot_BIN Avg_BIN FLAG_ISSIGNIFICANT  ;\n",
    "VAR LOS ClaimAmt;\n",
    "TABLE (FLAG_ISSIGNIFICANT*Avg_BIN='Change_AvgClaimAmt')ALL,Tot_BIN='Change_TotClaimAmt'*LOS*(MEAN) ALL PCTN;\n",
    "WHERE &GROUP. = &LATEYEAR.;\n",
    "RUN;\n",
    "\n",
    "TITLE3 Average Claim Amount ;\n",
    "PROC TABULATE DATA= JOINED ;\n",
    "CLASS Tot_BIN Avg_BIN FLAG_ISSIGNIFICANT  ;\n",
    "VAR LOS ClaimAmt;\n",
    "TABLE (FLAG_ISSIGNIFICANT*Avg_BIN='Change_AvgClaimAmt')ALL,Tot_BIN='Change_TotClaimAmt'*ClaimAmt*(MEAN) ALL PCTN;\n",
    "WHERE &GROUP. = &LATEYEAR.;\n",
    "RUN;\n",
    "\n",
    "PROC DELETE DATA= JOINED HOSP_TAG BSTATS STATS NPAR; RUN;\n",
    "\n",
    "footnote;\n",
    "title2;\n",
    "title3;\n",
    "ODS GRAPHICS ON;\n",
    "%mend;\n",
    "\n",
    "\n",
    "%macro NONPARAMETRIC_INDIVIDUAL_TEST(DSNAME=,VAR=,WHERE=,GROUP=);\n",
    "\n",
    "ODS GRAPHICS ON;\n",
    "\n",
    "PROC MEANS DATA=&DSNAME. N MEAN MEDIAN SUM;\n",
    "CLASS &GROUP.;\n",
    "VAR &VAR.;\n",
    "WHERE &WHERE.;\n",
    "RUN;\n",
    "%put here;\n",
    "\n",
    "PROC SGPLOT DATA=&DSNAME. ;\n",
    "  WHERE &WHERE. ;   \n",
    "  HISTOGRAM &VAR. / GROUP=&GROUP. TRANSPARENCY=0.5 ;       \n",
    "  DENSITY &VAR. / TYPE=KERNEL GROUP=&GROUP.; \n",
    "RUN;\n",
    "\n",
    "PROC NPAR1WAY DATA=&DSNAME.  Wilcoxon EDF  ;\n",
    "CLASS &GROUP. ;\n",
    "VAR &VAR.;\n",
    "WHERE  &WHERE.;  \n",
    "RUN;\n",
    "\n",
    "%mend;\n"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "<font face=\"Verdana, cursive, sans-serif\" >\n",
    "    \n",
    "Just to keep thing simple and easy, we compare Y-o-Y data"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "DATA YEAR_1617 ;\n",
    "SET CLAIMDATA;\n",
    "IF YEAR IN (2016 2017) THEN OUTPUT YEAR_1617;\n",
    "RUN;\n",
    "\n",
    "*************************************\n",
    "Compare claim 2016 vs. 2017\n",
    "*************************************;\n",
    "TITLE1 Non-Parametric Tests: Y-o-Y Comparison ;\n",
    "%NONPARAMETRIC_TESTS(DSNAME=YEAR_1617,\n",
    "                    VAR=CLAIMAMT,\n",
    "                    BY=PROVIDER,\n",
    "                    GROUP=YEAR);\n",
    "                    \n",
    "PROC PRINT DATA=NONPARA_TEST;RUN;\n",
    "\n",
    "*************************************\n",
    "To deepdive into 1 provider\n",
    "*************************************;\n",
    "%LET PROVIDER = \"A\";\n",
    "TITLE1 Claim Behavior of Provider = &PROVIDER. ;\n",
    "%NONPARAMETRIC_INDIVIDUAL_TEST(DSNAME=YEAR_1617,\n",
    "                                VAR=CLAIMAMT,\n",
    "                                WHERE=PROVIDER=&PROVIDER.,\n",
    "                                GROUP=YEAR);"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "<font face=\"Verdana, cursive, sans-serif\" >\n",
    "<img src=\"./images/providers_quad.PNG\" >\n",
    "    \n",
    "2 out of 8 providers shows evidence of morphing behavior 2016 vs.2017. 1 provider in partucular has is an extremist, with >50% AND >90% increase in average claim and total claim, respectively. "
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "<font face=\"Verdana, cursive, sans-serif\" >\n",
    "<H3>Conclusion</H3>\n",
    "\n",
    "We only shows here how to quickly identify and shortlist potential provider for auditing process. \n",
    "What is not shown here is a deepdive into the particular provider. Please refer to <a href=\"https://analytics.knowledgehub.ageas.com/posts/2012571-sas-by-example-how-to-quickly-identify-potential-changes-in-the-behavior-of-a-s?version=33244\">Ageas Knowledge Hub</a>  for more discussion. \n",
    "\n",
    "\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "SAS",
   "language": "sas",
   "name": "sas"
  },
  "language_info": {
   "codemirror_mode": "sas",
   "file_extension": ".sas",
   "mimetype": "text/x-sas",
   "name": "sas"
  },
  "varInspector": {
   "cols": {
    "lenName": 16,
    "lenType": 16,
    "lenVar": 40
   },
   "kernels_config": {
    "python": {
     "delete_cmd_postfix": "",
     "delete_cmd_prefix": "del ",
     "library": "var_list.py",
     "varRefreshCmd": "print(var_dic_list())"
    },
    "r": {
     "delete_cmd_postfix": ") ",
     "delete_cmd_prefix": "rm(",
     "library": "var_list.r",
     "varRefreshCmd": "cat(var_dic_list()) "
    }
   },
   "types_to_exclude": [
    "module",
    "function",
    "builtin_function_or_method",
    "instance",
    "_Feature"
   ],
   "window_display": false
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
