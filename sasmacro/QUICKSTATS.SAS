%macro QUICK_LEFTJOIN(LTAB,RTAB,KEY,OUT=JOINED);
PROC SQL;
CREATE TABLE &OUT. AS
(
SELECT *
FROM &LTAB. LTAB
LEFT JOIN &RTAB. RTAB
ON LTAB.&KEY.=RTAB.&KEY.
) 
;RUN;QUIT;
%mend;

%macro CLEAN_DSLABEL(LIBNAME, DSNAME);
PROC DATASETS LIBRARY=&LIBNAME. NOLIST; 
	MODIFY &DSNAME.; 
	ATTRIB _ALL_ LABEL=''; 
RUN; QUIT;
%mend;

%macro QUICKSTATS(DSNAME,REPORTNAME=MYDATA,NLIMIT=20);

PROC CONTENTS DATA=&DSNAME. OUT=CONT(KEEP=LIBNAME MEMNAME NAME TYPE INFORMAT FORMAT) NOPRINT;RUN;

PROC MEANS DATA=&DSNAME. NOPRINT  ;
VAR _NUMERIC_;
OUTPUT OUT=QSTATS(DROP=_TYPE_ _FREQ_);
RUN;

PROC TRANSPOSE DATA=QSTATS OUT=QSTATS_T(RENAME=(COL1=N COL2=MIN  COL3=MAX COL4=MEAN COL5=STD));RUN;

ODS OUTPUT NLEVELS=LEVELS;
PROC FREQ DATA=&DSNAME. NLEVELS ;
TABLE _ALL_ / NOPRINT MISSING;
RUN;

PROC SQL;
CREATE TABLE &REPORTNAME._QSTATS_NM AS
(SELECT *
FROM CONT LTAB
LEFT JOIN QSTATS_T RTAB_1 ON LTAB.NAME=RTAB_1._NAME_
LEFT JOIN LEVELS RTAB_2 ON LTAB.NAME=RTAB_2.TableVar
);
RUN; QUIT;

DATA &REPORTNAME._QSTATS_NM(DROP=_NAME_ _LABEL_ TableVar TableVarLabel  );
SET &REPORTNAME._QSTATS_NM;
RUN;
%CLEAN_DSLABEL(WORK,&REPORTNAME._QSTATS_NM);
PROC SORT DATA=&REPORTNAME._QSTATS_NM; BY TYPE NAME; RUN;

%ARRAY(CHARLIST,DATA=&REPORTNAME._QSTATS_NM(WHERE=(TYPE=2 AND NLEVELS<&NLIMIT.)),VAR=NAME);
%LET CHARLIST=%DO_OVER(CHARLIST,PHRASE=?);

/**
<b style="color:red;">Private Method</b>.
*/
%macro ONEWAYFREQS(CHAR);
	ODS OUTPUT ONEWAYFREQS=OWF_&CHAR.;
		PROC FREQ DATA=&DSNAME. ORDER=FREQ ;
		TABLE &CHAR. / MISSING NOCUM;
	RUN;

	DATA OWF_&CHAR.;
		SET OWF_&CHAR.;
		DROP TABLE F_&CHAR.;
		INFORMAT CLASS  $32.;
		INFORMAT NAME  $32.;
		CLASS = &CHAR.;
		NAME="&CHAR";
		DROP  &CHAR.;
	RUN;
%mend;
%DO_OVER(CHARLIST,MACRO=ONEWAYFREQS);

DATA ONEWAYFREQ;
SET %DO_OVER(CHARLIST,PHRASE=OWF_?);
RUN;
PROC DELETE DATA=%DO_OVER(VALUES=&CHARLIST.,PHRASE=OWF_?); RUN;

PROC SQL;
CREATE TABLE &REPORTNAME._QSTATS_CH AS
(SELECT *
FROM CONT(WHERE=(TYPE=2)) LTAB
LEFT JOIN ONEWAYFREQ RTAB_1 ON LTAB.NAME=RTAB_1.NAME
WHERE NOT MISSING(CLASS)
);
RUN; QUIT;
%CLEAN_DSLABEL(WORK,&REPORTNAME._QSTATS_CH);
PROC SORT DATA=&REPORTNAME._QSTATS_CH; BY TYPE NAME; RUN;

PROC DELETE DATA=QSTATS QSTATS_T CONT ONEWAYFREQ LEVELS; RUN;

%mend;